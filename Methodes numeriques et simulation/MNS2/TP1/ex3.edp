//Nombre de mailles suivant x et y

int Nbnoeuds = 2;
real cpu = clock();

//Donn√©es
real K = 1e5;
real S = 0.5;
real D = 2e-5;
real Text = 8;

//Definition du maillage
border C1(t = 0., 0.3){x = t; y = 0.;}
border C2(t = 0., 0.7){x = 0.3; y = t;}
border C3(t = 0.3, 0.7){x = t; y = 0.7;}
border C4(t = 0.3, 1.){x = 0.7; y = 1. - t;}
border C5(t = 0.7, 1.){x = t; y = 0.;}
border C6(t = 0., 1.){x = 1.; y = t;}
border C7(t = 0., 1.){x = 1 - t; y = 1.;}
border C8(t = 0., 1.){x = 0.; y = 1 - t;}

mesh Th = buildmesh(C1(3*Nbnoeuds) + C2(7*Nbnoeuds) + C3(4*Nbnoeuds) + C4(7*Nbnoeuds)
        + C5(3*Nbnoeuds) + C6(10*Nbnoeuds) + C7(10*Nbnoeuds) + C8(10*Nbnoeuds));
cout << "Temps CPU apres def du domaine = " << (clock() - cpu) << endl;

//Fonction de x et de y
func f = S*(x < 0.95)*(x > 0.85)*(y < 0.95)*(y > 0.85);

//Definition de l'espace des elements finis P1 associe au maillage Th
fespace Vh(Th, P1);
cout << "Temps CPU apres def du maillage = " << (clock() - cpu) << endl;

//uh et vh sont des elements de Vh
Vh uh, vh;

//Definition du probleme variationnel

problem laplacien(uh, vh, solver = LU) = int1d(Th)(D*K*uh*vh) - int1d(Th)(D*K*Text*vh) + int2d(Th)(D*dx(uh)*dx(vh)
        + D*dy(uh)*dy(vh)) - int2d(Th)(f*vh) ;

cout << "Temps CPU apres def du probleme var = " << (clock() - cpu) << endl;


//Resolution du probleme
laplacien;
cout << "Temps CPU apres resolution = " << (clock() - cpu) << endl;

//On visualise le maillage et le resultat
plot(Th, wait=true);
plot(uh, wait = true);